plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id 'kotlin-kapt'
  id 'dagger.hilt.android.plugin'

  id 'com.google.gms.google-services' // sed:free-build:remove
  id 'com.google.firebase.crashlytics' // sed:free-build:remove

  id 'com.adarshr.test-logger' version '3.0.0'
}

apply from: 'jacoco.gradle'

android {
  compileSdkVersion config.compileSdkVersion

  defaultConfig {
    applicationId 'com.github.ashutoshgngwr.noice'
    minSdkVersion config.minSdkVersion
    targetSdkVersion config.compileSdkVersion
    versionCode 52
    versionName '1.3.3'
    testInstrumentationRunner 'com.github.ashutoshgngwr.noice.InstrumentedTestRunner'
    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
  }

  signingConfigs {
    release {
      storeFile file('../keystore.jks')
      keyAlias "$System.env.KEY_ALIAS"
      storePassword "$System.env.STORE_PASSWORD"
      keyPassword "$System.env.KEY_PASSWORD"
    }
  }

  buildFeatures {
    viewBinding true
    dataBinding true
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }

    release {
      shrinkResources true
      minifyEnabled true
      signingConfig = signingConfigs.release // sed:free-build:remove
    }
  }

  flavorDimensions 'tier'
  productFlavors {
    free {
      dimension 'tier'
      buildConfigField 'boolean', 'IS_FREE_BUILD', 'true'
    }

    full {
      dimension 'tier'
      buildConfigField 'boolean', 'IS_FREE_BUILD', 'false'
    }
  }

  compileOptions {
    sourceCompatibility = config.javaCompatibility
    targetCompatibility = config.javaCompatibility
  }

  kotlinOptions {
    useIR = true
    jvmTarget = config.kotlinJvmTarget
    freeCompilerArgs += ["-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
      all {
        testlogger.theme 'mocha'
      }
    }
  }
}

dependencies {
  def fragmentVersion = '1.3.6'
  // TODO: move this to stable release. The latest stable release (2.3.5) causes a crash with error
  //  `java.lang.RuntimeException: Unknown animation name: x`
  //  Although, the crash only occurs on the Play Store variant..?
  def navVersion = '2.4.0-alpha06'

  def espressoVersion = '3.3.0'
  def mockkVersion = '1.12.2'

  kapt "com.google.dagger:hilt-compiler:$deps.hiltVersion"

  implementation 'androidx.activity:activity-ktx:1.3.1'
  implementation 'androidx.appcompat:appcompat:1.3.1'
  implementation 'androidx.browser:browser:1.3.0'
  implementation 'androidx.cardview:cardview:1.0.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
  implementation 'androidx.core:core-ktx:1.6.0'
  implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
  implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
  implementation "androidx.lifecycle:lifecycle-common-java8:$deps.lifecycleVersion"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:$deps.lifecycleVersion"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$deps.lifecycleVersion"
  implementation 'androidx.media:media:1.4.1'
  implementation 'androidx.mediarouter:mediarouter:1.2.5'
  implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
  implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
  implementation 'androidx.preference:preference-ktx:1.1.1'
  implementation 'androidx.recyclerview:recyclerview:1.2.1'
  implementation 'com.github.AppIntro:AppIntro:6.1.0'
  implementation 'com.google.android.exoplayer:exoplayer-core:2.13.3'
  implementation 'com.google.android.flexbox:flexbox:3.0.0'
  implementation 'com.google.android.material:material:1.4.0'
  implementation 'com.google.code.gson:gson:2.8.9'
  implementation "com.google.dagger:hilt-android:$deps.hiltVersion"
  implementation 'com.ncorti:slidetoact:0.9.0'
  implementation 'io.github.medyo:android-about-page:2.0.0'
  implementation 'io.github.mrherintsoahasina:flextools:1.0.3'
  implementation 'io.noties.markwon:core:4.6.2'
  implementation 'org.greenrobot:eventbus:3.3.1'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$deps.kotlinVersion"

  implementation project(":api-client")

  fullImplementation 'com.android.billingclient:billing-ktx:4.0.0'
  fullImplementation 'com.google.android.gms:play-services-base:18.0.1'
  fullImplementation 'com.google.android.gms:play-services-cast-framework:20.0.0'
  fullImplementation 'com.google.android.play:core:1.10.1'
  fullImplementation platform('com.google.firebase:firebase-bom:29.0.3')
  fullImplementation 'com.google.firebase:firebase-analytics-ktx'
  fullImplementation 'com.google.firebase:firebase-crashlytics-ktx'
  fullImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$deps.coroutinesVersion"


  kaptTest "com.google.dagger:hilt-android-compiler:$deps.hiltVersion"

  testImplementation "androidx.test:core:$deps.testCoreVersion"
  testImplementation "com.google.dagger:hilt-android-testing:$deps.hiltVersion"
  testImplementation "io.mockk:mockk:$mockkVersion"
  testImplementation "junit:junit:$deps.junitVersion"
  testImplementation "org.robolectric:robolectric:$deps.robolectricVersion"
  testImplementation 'org.skyscreamer:jsonassert:1.5.0'

  testFullImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$deps.coroutinesVersion"


  kaptAndroidTest "com.google.dagger:hilt-android-compiler:$deps.hiltVersion"

  androidTestImplementation "androidx.test:core:$deps.testCoreVersion"
  androidTestImplementation "androidx.test:rules:1.4.0"
  androidTestImplementation "androidx.test:runner:1.4.0"
  androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
  androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
  androidTestImplementation 'androidx.test.ext:junit:1.1.3'
  androidTestImplementation "com.google.dagger:hilt-android-testing:$deps.hiltVersion"
  androidTestImplementation "io.mockk:mockk-android:$mockkVersion"
  androidTestImplementation "junit:junit:$deps.junitVersion"

  androidTestFullImplementation 'tools.fastlane:screengrab:2.1.1'
}
