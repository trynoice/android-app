plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'org.jetbrains.kotlin.kapt'
  id 'com.google.devtools.ksp'

  id 'androidx.navigation.safeargs.kotlin'
  id 'com.google.dagger.hilt.android'
  id 'com.mikepenz.aboutlibraries.plugin'

  id 'com.google.gms.google-services' // sed:free-build:remove
  id 'com.google.firebase.crashlytics' // sed:free-build:remove

  id 'com.adarshr.test-logger'
  id 'jacoco'
}

android {
  defaultConfig {
    namespace 'com.github.ashutoshgngwr.noice'
    applicationId 'com.github.ashutoshgngwr.noice'
    versionCode 71
    versionName '2.5.6'
    testInstrumentationRunner 'com.github.ashutoshgngwr.noice.InstrumentedTestRunner'
    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
  }

  signingConfigs {
    release {
      storeFile file('../keystore.jks')
      keyAlias "$System.env.KEY_ALIAS"
      storePassword "$System.env.STORE_PASSWORD"
      keyPassword "$System.env.KEY_PASSWORD"
    }
  }

  buildFeatures {
    buildConfig true
    dataBinding true
    viewBinding true
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }

    release {
      shrinkResources true
      minifyEnabled true
      signingConfig = signingConfigs.release // sed:free-build:remove
    }
  }

  flavorDimensions += 'tier'
  productFlavors {
    free {
      dimension 'tier'
      buildConfigField 'boolean', 'IS_FREE_BUILD', 'true'
    }

    full {
      dimension 'tier'
      buildConfigField 'boolean', 'IS_FREE_BUILD', 'false'
    }
  }

  kotlinOptions {
    freeCompilerArgs += ["-opt-in=androidx.paging.ExperimentalPagingApi"]
  }

  ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
  }

  packagingOptions {
    // https://github.com/Kotlin/kotlinx.coroutines#avoiding-including-the-debug-infrastructure-in-the-resulting-apk
    resources.excludes += "DebugProbesKt.bin"
  }
}

project.afterEvaluate {
  tasks.register('testDebugCoverage', JacocoReport) {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports for the all debug build variants.'

    android.applicationVariants
      .findAll { it.buildType.name == "debug" }
      .forEach { variant -> dependsOn "test${variant.name.capitalize()}Coverage" }
  }
}

dependencies {
  def androidXHiltVersion = '1.1.0'
  def fragmentVersion = '1.6.1'
  def media3Version = '1.2.0'
  def roomVersion = '2.6.1'
  def workManagerVersion = '2.9.0'

  def espressoVersion = '3.5.1'
  def mockkVersion = '1.13.9'

  kapt "com.google.dagger:hilt-compiler:$deps.hiltVersion"
  kapt "androidx.hilt:hilt-compiler:$androidXHiltVersion"
  ksp "androidx.room:room-compiler:$roomVersion"

  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'androidx.browser:browser:1.7.0'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
  implementation "androidx.core:core-ktx:$deps.androidXCoreVersion"
  implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
  implementation "androidx.hilt:hilt-work:$androidXHiltVersion"
  implementation "androidx.lifecycle:lifecycle-common-java8:$deps.lifecycleVersion"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$deps.lifecycleVersion"
  implementation 'androidx.media:media:1.7.0' // needed for audio focus management.
  implementation "androidx.media3:media3-common:$media3Version"
  implementation "androidx.media3:media3-exoplayer:$media3Version"
  implementation "androidx.media3:media3-exoplayer-workmanager:$media3Version"
  implementation "androidx.media3:media3-session:$media3Version"
  implementation "androidx.navigation:navigation-fragment-ktx:$deps.navVersion"
  implementation "androidx.navigation:navigation-ui-ktx:$deps.navVersion"
  implementation 'androidx.paging:paging-runtime-ktx:3.2.1'
  implementation 'androidx.preference:preference-ktx:1.2.1'
  implementation 'androidx.recyclerview:recyclerview:1.3.2'
  implementation "androidx.room:room-ktx:$roomVersion"
  implementation "androidx.room:room-paging:$roomVersion"
  implementation "androidx.room:room-runtime:$roomVersion"
  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
  implementation "androidx.work:work-runtime-ktx:$workManagerVersion"
  implementation 'com.caverock:androidsvg-aar:1.4'
  implementation 'com.github.AppIntro:AppIntro:6.3.1'
  implementation 'com.google.android.flexbox:flexbox:3.0.0'
  implementation 'com.google.android.material:material:1.11.0'
  implementation 'com.google.code.gson:gson:2.10.1'
  implementation "com.google.dagger:hilt-android:$deps.hiltVersion"
  implementation "com.mikepenz:aboutlibraries:${deps.aboutLibrariesVersion}"
  implementation 'io.github.medyo:android-about-page:2.0.0'
  implementation 'io.noties.markwon:core:4.6.2'
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$deps.coroutinesVersion"
  implementation project(":api-client")

  fullImplementation 'androidx.mediarouter:mediarouter:1.6.0'
  fullImplementation 'com.android.billingclient:billing-ktx:6.1.0'
  fullImplementation 'com.google.android.gms:play-services-base:18.2.0'
  fullImplementation 'com.google.android.gms:play-services-cast-framework:21.4.0'
  fullImplementation 'com.google.android.play:core:1.10.3'
  fullImplementation platform('com.google.firebase:firebase-bom:32.7.0')
  fullImplementation 'com.google.firebase:firebase-analytics-ktx'
  fullImplementation 'com.google.firebase:firebase-crashlytics-ktx'


  kaptTest "com.google.dagger:hilt-android-compiler:$deps.hiltVersion"

  testImplementation "androidx.media3:media3-test-utils:$media3Version"
  testImplementation "androidx.media3:media3-test-utils-robolectric:$media3Version"
  testImplementation "androidx.test:core:$deps.testCoreVersion"
  testImplementation "com.google.dagger:hilt-android-testing:$deps.hiltVersion"
  testImplementation "io.mockk:mockk-jvm:$mockkVersion"
  testImplementation "junit:junit:$deps.junitVersion"
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$deps.coroutinesVersion"
  testImplementation "org.robolectric:robolectric:$deps.robolectricVersion"


  kaptAndroidTest "com.google.dagger:hilt-android-compiler:$deps.hiltVersion"

  androidTestImplementation "androidx.test:core:$deps.testCoreVersion"
  androidTestImplementation "androidx.test:rules:1.5.0"
  androidTestImplementation "androidx.test:runner:1.5.2"
  androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
  androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
  androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
  androidTestImplementation 'androidx.test.ext:junit:1.1.5'
  androidTestImplementation "androidx.work:work-testing:$workManagerVersion"
  androidTestImplementation "com.google.dagger:hilt-android-testing:$deps.hiltVersion"
  androidTestImplementation "junit:junit:$deps.junitVersion"

  androidTestImplementation("io.mockk:mockk-android:$mockkVersion") {
    exclude group: 'org.junit.jupiter' // causes build errors due to duplicate files in META-INF
  }
}
