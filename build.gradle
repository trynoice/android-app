plugins {
  id 'com.android.application' version "${agpVersion}" apply false
  id 'com.android.library' version "${agpVersion}" apply false
  id 'org.jetbrains.kotlin.android' version "${kotlinVersion}" apply false
  id 'org.jetbrains.kotlin.kapt' version "${kotlinVersion}" apply false

  id 'androidx.navigation.safeargs.kotlin' version "${navVersion}" apply false
  id 'com.google.dagger.hilt.android' version "${hiltVersion}" apply false
  id 'com.mikepenz.aboutlibraries.plugin' version "${aboutLibrariesVersion}" apply false

  id 'com.google.gms.google-services' version '4.3.15' apply false // sed:free-build:remove
  id 'com.google.firebase.crashlytics' version '2.9.5' apply false // sed:free-build:remove

  id 'com.adarshr.test-logger' version '3.2.0' apply false
}

// project-wide constants for sharing configuration and dependency versions across modules.
project.ext {
  deps = [
    aboutLibrariesVersion: "${aboutLibrariesVersion}",
    androidXCoreVersion  : '1.10.0',
    coroutinesVersion    : '1.7.2',
    hiltVersion          : "${hiltVersion}",
    kotlinVersion        : "${kotlinVersion}",
    lifecycleVersion     : '2.6.1',
    navVersion           : "${navVersion}",

    junitVersion         : '4.13.2',
    robolectricVersion   : '4.9.2',
    testCoreVersion      : '1.5.0',
  ]
}

subprojects {
  afterEvaluate { subproject ->
    if (!subproject.hasProperty("android")) {
      return
    }

    android {
      compileSdk 33

      defaultConfig {
        minSdk 21
        targetSdk 33
      }

      compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
      }

      kotlin {
        jvmToolchain 11
      }

      kotlinOptions {
        freeCompilerArgs += ["-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"]
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
